PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Added notes to already existing concepts
#
SELECT DISTINCT ?concept ?conceptLabel ?oldNote ?newNote ?found ?ysoConcept ?ysoConceptLabel ?ysoNote
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ?oldVersion ?newVersion ) {
    ( <http://www.yso.fi/onto/ysa/version> skos:Concept  "20150408" "20150508" )
  }
  GRAPH ?versionHistoryGraph {
    # get the current and the previous version as default versions
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?previousVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?latestVersion .
    # select the versions to actually use
    BIND(coalesce(?oldVersion, ?previousVersion) AS ?oldVersionSelected)
    BIND(coalesce(?newVersion, ?latestVersion) AS ?newVersionSelected)
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersionSelected ;
      sh:deltaTo/dc:identifier ?newVersionSelected ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
  }
  
  OPTIONAL {
    # newly inserted notes
    GRAPH ?insertionsGraph {
      ?concept skos:note|skos:scopeNote ?newNote .
    }
  }

  OPTIONAL {
    # deleted notes
    GRAPH ?deletionsGraph {
      ?concept skos:note|skos:scopeNote ?oldNote .
    }
  }
  
  FILTER (BOUND(?newNote) || BOUND(?oldNote))
  FILTER (!(BOUND(?newNote) && BOUND(?oldNote) && ?newNote = ?oldNote))
  
  # ... for already existing concepts
  FILTER exists {
    GRAPH ?oldVersionGraph {
      ?concept ?p []
    }
  }
  
  GRAPH ?newVersionGraph {
      ?concept skos:prefLabel ?conceptLabel .
  }

  # current note in YSO
  SERVICE <http://api.dev.finto.fi/sparql> {
    OPTIONAL {
      GRAPH <http://www.yso.fi/onto/yso/> {
        ?ysoConcept skos:closeMatch ?concept .
        FILTER NOT EXISTS { ?ysoConcept owl:deprecated true }
        ?ysoConcept skos:prefLabel ?ysoConceptLabel .
        FILTER(LANG(?conceptLabel)=LANG(?ysoConceptLabel))
        BIND("yso" AS ?found)
        OPTIONAL {
          ?ysoConcept skos:note ?ysoNote .
          FILTER(LANG(?ysoNote) = LANG(?ysoConceptLabel))
        }
      }      
    }
  }



}
ORDER BY LCASE(?conceptLabel)
