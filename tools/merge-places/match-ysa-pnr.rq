PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pnrs: <http://ldf.fi/pnr-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT ?ysac ?labelFi ?labelSv 
(CONCAT(GROUP_CONCAT(?noteFi; separator='; '), '\n', GROUP_CONCAT(?noteSv; separator='; ')) AS ?note)
(GROUP_CONCAT(?bt; separator='; ') AS ?broader)
?method ?place 
(GROUP_CONCAT(DISTINCT ?pnrLabelFi; separator='; ') AS ?pnrFi)
(GROUP_CONCAT(DISTINCT ?pnrLabelSv; separator='; ') AS ?pnrSv)
?mapScale ?placetype 
(GROUP_CONCAT(DISTINCT ?parentLabel; separator='; ') AS ?parents)
(IRI(CONCAT('https://www.google.com/maps/@', STR(?lat), ',', STR(?long),',14z')) AS ?mapurl)
WHERE {
  {
    SELECT ?s ?ysac ?labelFi ?labelSv ?noteFi ?noteSv ?bt
    WHERE {
      GRAPH <http://www.yso.fi/onto/ysa/> {
        ?s skos:exactMatch ?ysac .
        FILTER(STRSTARTS(STR(?ysac), 'http://www.yso.fi/onto/ysa/'))
        ?s a skos:Concept .
        ?s skos:prefLabel ?labelFi .
        FILTER (LANG(?labelFi)='fi')
        OPTIONAL { 
          ?s skos:prefLabel ?lSv .
          FILTER (LANG(?lSv)='sv')
        }
        BIND(IF(BOUND(?lSv), ?lSv, '') AS ?labelSv)
        OPTIONAL {
          ?s skos:note ?noteFi .
          FILTER(LANG(?noteFi)='fi')
        }
        OPTIONAL {
          ?s skos:note ?noteSv .
          FILTER(LANG(?noteSv)='sv')
        }
        OPTIONAL {
          ?s skos:broader/skos:prefLabel ?bt .
          FILTER(LANG(?bt)='fi')
        }
      }
    }
    LIMIT 8000
  }
  GRAPH <http://ldf.fi/pnr/> {
    # identical prefLabel in Finnish
    OPTIONAL {
      ?p skos:prefLabel ?labelFi .
      BIND('label fi' as ?method)
    }
    # identical prefLabel in Swedish
    OPTIONAL {
      ?p skos:prefLabel ?labelSv .
      BIND('label sv' as ?method)
    }
    # altLabel matches in Finnish
    OPTIONAL {
      ?s skos:altLabel ?altFi .
      FILTER(LANG(?altFi)='fi')
      ?p skos:prefLabel ?altFi .
      BIND('altlabel fi' as ?method)
    }
    # altLabel matches in Swedish
    OPTIONAL {
      ?s skos:altLabel ?altSv .
      FILTER(LANG(?altSv)='sv')
      ?p skos:prefLabel ?altSv .
      BIND('altlabel sv' as ?method)
    }
    # "Sastamala -- Kukkuri" case: parent + child match (fi)
    OPTIONAL {
      BIND(STRBEFORE(?labelFi, " -- ") as ?parentLabelFi)
      BIND(STRAFTER(?labelFi, " -- ") as ?childLabelFi)
      ?p skos:prefLabel ?childLabelFi .
      ?parent skos:prefLabel ?parentLabelFi .
      ?p skos:broader+ ?parent .
      BIND('parent+child fi' as ?method)
    }
    # "Raseborg -- Fiskars" case: parent + child match (sv)
    OPTIONAL {
      BIND(STRBEFORE(?labelSv, " -- ") as ?parentLabelSv)
      BIND(STRAFTER(?labelSv, " -- ") as ?childLabelSv)
      ?p skos:prefLabel ?childLabelSv .
      ?p skos:broader+ ?parent .
      ?parent skos:prefLabel ?parentLabelSv .
      BIND('parent+child sv' as ?method)
    }
    # "Lylyjärvi -- Etelä-Savo" case: child + parent match (fi)
    OPTIONAL {
      BIND(STRAFTER(?labelFi, " -- ") as ?parentLabelFi)
      BIND(STRBEFORE(?labelFi, " -- ") as ?childLabelFi)
      ?p skos:prefLabel ?childLabelFi .
      ?parent skos:prefLabel ?parentLabelFi .
      ?p skos:broader+ ?parent .
      BIND('child+parent fi' as ?method)
    }
    # "XXX - YYY" case: child + parent match (sv)
    OPTIONAL {
      BIND(STRAFTER(?labelSv, " -- ") as ?parentLabelSv)
      BIND(STRBEFORE(?labelSv, " -- ") as ?childLabelSv)
      ?p skos:prefLabel ?childLabelSv .
      ?parent skos:prefLabel ?parentLabelSv .
      ?p skos:broader+ ?parent .
      BIND('child+parent sv' as ?method)
    }
    # "Kalvola -- Taljala" case: only the child part matches (fi)
    OPTIONAL {
      BIND(STRAFTER(?labelFi, " -- ") as ?childLabelFi)
      ?p skos:prefLabel ?childLabelFi .
      BIND('child-only fi' as ?method)
    }
    # "Dragsfjärd -- Vänö" case: only the child part matches (sv)
    OPTIONAL {
      BIND(STRAFTER(?labelSv, " -- ") as ?childLabelSv)
      ?p skos:prefLabel ?childLabelSv .
      BIND('child-only sv' as ?method)
    }
    
    BIND(IF(BOUND(?p), ?p, '') AS ?place)
    OPTIONAL {
      OPTIONAL {
        ?place pnrs:mapScale ?scale .
        BIND(STRDT(REPLACE(STR(?scale), STR(pnrs:map_scale_), ''), xsd:integer) AS ?mapScale)
      }
      ?place rdf:type/rdfs:label ?placetype .
      FILTER(LANG(?placetype)='fi')
      OPTIONAL {
        ?place skos:prefLabel ?pnrLabelFi .
        FILTER(LANG(?pnrLabelFi)='fi')
      }
      OPTIONAL {
        ?place skos:prefLabel ?pnrLabelSv .
        FILTER(LANG(?pnrLabelSv)='sv')
      }
      OPTIONAL {
        ?place skos:broader/skos:prefLabel ?parentLabel .
        FILTER(LANG(?parentLabel)='fi')
      }
      OPTIONAL { # fallback: any language
        ?place skos:broader/skos:prefLabel ?parentLabel .
      }
      OPTIONAL {
        ?place geo:lat ?lat .
        ?place geo:long ?long .
      }
    }
  }
} 
GROUP BY ?ysac ?labelFi ?labelSv ?method ?place ?mapScale ?placetype ?lat ?long
ORDER BY ?labelFi DESC(?mapScale) ?placetype
