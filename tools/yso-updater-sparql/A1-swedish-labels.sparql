PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ysa: <http://www.yso.fi/onto/ysa/>
PREFIX allars: <http://www.yso.fi/onto/allars/>
PREFIX yso: <http://www.yso.fi/onto/yso/>
PREFIX ysa-meta: <http://www.yso.fi/onto/ysa-meta/>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/2007-03-02/>
PREFIX yso-update: <http://www.yso.fi/onto/yso-update/>
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>

CONSTRUCT {
  ?ysoc a yso-meta:Concept .
  ?ysoc om:definedConcept ?ac .
  ?ysoc skos:prefLabel ?pref .
  ?ysoc yso-meta:oldLabel ?alt .
  ?ysoc yso-meta:ysaComment ?note .
  ?ysoc yso-meta:ysaSource ?source .
  ?ysoc rdfs:subClassOf yso-update:uudetSv .
}
WHERE {
  {
    SELECT ?ysoc ?ysac
    WHERE {
      ?ysoc a yso-meta:Concept .
      ?ysoc om:definedConcept ?ysac .
      FILTER(STRSTARTS(STR(?ysac), STR(ysa:)))
      FILTER NOT EXISTS {
        ?ysoc om:definedConcept ?ac .
        FILTER(STRSTARTS(STR(?ac), STR(allars:)))
      }
    }
  }
  SERVICE <http://api.dev.finto.fi/sparql> {
    GRAPH <http://www.yso.fi/onto/ysa/> {
      ?ysac skos:exactMatch ?ac .
      FILTER(STRSTARTS(STR(?ac), STR(allars:)))
    }
    GRAPH <http://www.yso.fi/onto/allars/> {
      ?ac skos:prefLabel ?pref .
      OPTIONAL { ?ac skos:altLabel ?alt }
      OPTIONAL { ?ac skos:scopeNote ?note }
      OPTIONAL { ?ac dc:source ?source }
    }
  }
}
