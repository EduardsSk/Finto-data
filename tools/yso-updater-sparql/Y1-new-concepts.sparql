PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ysa: <http://www.yso.fi/onto/ysa/>
PREFIX yso: <http://www.yso.fi/onto/yso/>
PREFIX ysa-meta: <http://www.yso.fi/onto/ysa-meta/>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/2007-03-02/>
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>

CONSTRUCT {
  ?newc om:definedConcept ?c .
  ?newc a yso-meta:Concept .
  ?newc skos:prefLabel ?pref .
  ?newc yso-meta:oldLabel ?alt .
  ?newc yso-meta:ysaComment ?note .
  ?newc yso-meta:ysaSource ?source .
  ?newc yso-meta:hasThematicGroup ?ysogroup .
}
WHERE {
  {
    SELECT ?c
    WHERE {
      SERVICE <http://api.dev.finto.fi/sparql> {
        GRAPH <http://www.yso.fi/onto/ysa/> {
          ?c a skos:Concept .
          ?c dct:created ?created .
          FILTER NOT EXISTS { ?c a ysa-meta:GeographicalConcept }
          FILTER ( ?created < "2014-01-01"^^xsd:date )
        }
      }
      FILTER NOT EXISTS {
        ?ysoc om:definedConcept ?c .
      }
    }
  }
  BIND(IRI(REPLACE(STR(?c), STR(ysa:), STR(yso:))) as ?newc)
  SERVICE <http://api.dev.finto.fi/sparql> {
    GRAPH <http://www.yso.fi/onto/ysa/> {
      ?c skos:prefLabel ?pref .
      OPTIONAL { ?c skos:altLabel ?alt }
      OPTIONAL { ?c skos:scopeNote ?note }
      OPTIONAL { ?c dc:source ?source }
      OPTIONAL { ?ysagroup skos:member ?c }
    }  
  }
  OPTIONAL { ?ysogroup om:definedConcept ?ysagroup }
}
