PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ysa-meta: <http://www.yso.fi/onto/ysa-meta/>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/2007-03-02/>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>

CONSTRUCT {
  ?ysoc skos:altLabel ?ysalabel .
}
WHERE {
  SERVICE <http://sparql.dev.finto.fi/skos-history/sparql> {
    GRAPH <http://www.yso.fi/onto/ysa/version> {
      # find the graph URI of the latest available version of YSA
      ?versionset dsv:currentVersionRecord/sh:usingNamedGraph/sd:name ?ysaNewVersionGraph .
    }
    GRAPH ?ysaNewVersionGraph {
      ?ysac a skos:Concept .
      ?ysac skos:altLabel ?ysalabel .
      FILTER NOT EXISTS { ?ysac a ysa-meta:GeographicalConcept }
    }
  }
  ?ysoc om:definedConcept ?ysac .
  FILTER NOT EXISTS {
    ?anyysoc ?anylabel ?ysalabel .
    # Special case: it's okay to add an altLabel that is identical
    # to a prefLabel of the same concept; this happens when the YSA prefLabel
    # has been changed and the old YSA prefLabel has become an altLabel.
    # Most likely the same change will be soon made in YSO too.
    # Skosify will drop altLabels that are identical to prefLabels from the
    # published version of YSO.
    #
    # TODO: disabled for now due to not working with sdbquery
    # FILTER (?anyysoc != ?ysoc || ?anylabel != skos:prefLabel)
  }
  FILTER NOT EXISTS {
    ?ysoc2 om:definedConcept ?ysac .
    FILTER (?ysoc != ?ysoc2)
    FILTER NOT EXISTS { ?ysoc2 a yso-meta:DeprecatedConcept }
  }
  FILTER NOT EXISTS { ?ysoc a yso-meta:DeprecatedConcept }
}
