PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ysa: <http://www.yso.fi/onto/ysa/>
PREFIX allars: <http://www.yso.fi/onto/allars/>
PREFIX yso: <http://www.yso.fi/onto/yso/>
PREFIX ysa-meta: <http://www.yso.fi/onto/ysa-meta/>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/2007-03-02/>
PREFIX yso-update: <http://www.yso.fi/onto/yso-update/>
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>

CONSTRUCT {
  ?ysoc rdfs:subClassOf yso-update:poistuneetYSA .
}
WHERE {
  ?ysoc om:definedConcept ?ysac .
  FILTER NOT EXISTS { ?ysoc a yso-meta:DeprecatedConcept }
  FILTER(STRSTARTS(STR(?ysac), STR(ysa:)))

  MINUS {
    SERVICE <http://sparql.dev.finto.fi/skos-history/sparql> {
      GRAPH <http://www.yso.fi/onto/ysa/version> {
        # find the graph URI of the latest available version of YSA
        ?versionset dsv:currentVersionRecord/sh:usingNamedGraph/sd:name ?ysaNewVersionGraph .
      }
      GRAPH ?ysaNewVersionGraph {
        ?ysac a []
      }
    }
  }
}
