PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>
 
CONSTRUCT {
 ?yso skos:exactMatch ?match .
}
WHERE
{
  {
    ?yso om:definedConcept ?match .
    FILTER (regex(str(?match), "^http://www.yso.fi/onto/ysa/"))
    FILTER NOT EXISTS {
      ?yso om:definedConcept ?match2 .
      FILTER(regex(str(?match2), "^http://www.yso.fi/onto/ysa/"))
      FILTER(?match != ?match2)
    }
  } 
  UNION
  {
    ?yso a <http://www.yso.fi/onto/yso-meta/2007-03-02/Concept> .
    ?yso om:definedConcept ?match .
    FILTER (regex(str(?match), "^http://www.yso.fi/onto/allars/"))
    FILTER NOT EXISTS {
      ?yso om:definedConcept ?match2 .
      FILTER(regex(str(?match2), "^http://www.yso.fi/onto/allars/"))
      FILTER(?match != ?match2)
    }
  }
}
